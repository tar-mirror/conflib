dnl Process this file with autoconf to produce a configure script.
AC_INIT(doc/conflib.texi)
AM_INIT_AUTOMAKE(conflib,0.4.5)
AM_CONFIG_HEADER(config.h)
AM_PROG_LIBTOOL

dnl CURRENT[:REVISION[:AGE]]
dnl inkompatible Aenderungen: AGE und REV auf 0 setzen sowie CURRENT erhoehen
dnl kompatible Aenderungen: CURRENT++, REV=0, AGE++
SOVERSION=6:0:1
AC_ARG_PROGRAM

PACKAGE_VERSION="$PACKAGE-$VERSION"
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AC_DEFINE_UNQUOTED(PACKAGE_VERSION, "$PACKAGE_VERSION")
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(PACKAGE_VERSION)

ALL_LINGUAS="de"

AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(README_ALPHA)
AC_SUBST(SOVERSION)

changequote(,)dnl
case $VERSION in
	[0-9]*.[0-9]*.[0-9]*) README_ALPHA="README-alpha";;
	*) README_ALPHA=;;
esac
changequote([, ])dnl

dnl Checks for programs.
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
dnl AC_PROG_RANLIB obsolete wegen AM_PROG_LIBTOOL
dnl AC_PATH_PROGS(AR,ar,:) obsolete wegen AM_PROG_LIBTOOL
AC_PROG_MAKE_SET
AC_CHECK_PROGS(M4,gm4 gnum4 m4,:)
AC_CHECK_PROGS(TAR,gtar tar,:)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h pwd.h string.h fcntl.h limits.h)
AC_CHECK_HEADERS(rx.h regex.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
dnl AC_C_CROSS obsolete, in AC_PROG_CC enthalten.
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(putenv setenv unsetenv)
AC_CHECK_FUNCS(regcomp)
if test "x$ac_cv_func_regcomp" = "xno" ; then
	AC_MSG_WARN("will not use regular expressions")
fi
dnl strcase*cmp and common misspellings
AC_CHECK_FUNCS(strcasecmp strncasecmp stricmp strnicmp)
AC_CHECK_FUNCS(strdup strtol strerror)

AC_MSG_CHECKING([for working fnmatch function])
AC_CACHE_VAL(fu_cv_sys_working_fnmatch,
[AC_TRY_RUN([
#include <fnmatch.h>
int
main ()
{
    exit ((fnmatch ("a*", "abc", FNM_PATHNAME) == 0
        && fnmatch ("a", "b", FNM_PATHNAME) == FNM_NOMATCH)
        ? 0 : 1);
}],
fu_cv_sys_working_fnmatch=yes,
fu_cv_sys_working_fnmatch=no,
fu_cv_sys_working_fnmatch=no)])
if test $fu_cv_sys_working_fnmatch = no; then
	:
else
	AC_DEFINE(HAVE_FNMATCH)
fi
AC_MSG_RESULT($fu_cv_sys_working_fnmatch)
UO_FUNC_BROKEN_F_SETLK

AC_SUBST(LIBOBJS)
AC_SUBST(ERSATZHEADER)

AC_OUTPUT(Makefile tests/Makefile doc/Makefile src/Makefile testsuite/Makefile)
